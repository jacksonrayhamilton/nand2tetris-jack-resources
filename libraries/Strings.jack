/**
 * Various utility methods for working with strings.
 *
 * @author Jackson Ray Hamilton
 */
class Strings {

  /**
   * @return Whether or not `a` and `b` are the same string.
   */
  function boolean equals(String a, String b) {
    var int aLength, bLength;
    var int i;

    let aLength = a.length();
    let bLength = b.length();

    // Do the lengths match?
    if (~(aLength = bLength)) {
      return false;
    }

    // Are all the chars the same?
    let i = 0;
    while (i < aLength) {
      if (~(a.charAt(i) = b.charAt(i))) {
        return false;
      }
      let i = i + 1;
    }

    return true;
  }

  /**
   * Mutates a string buffer by adding a sequence of characters to it.
   */
  function void appendToBuffer(String buffer, String string) {
    var int i, length;
    let i = 0;
    let length = string.length();
    while (i < length) {
      do buffer.appendChar(string.charAt(i));
      let i = i + 1;
    }
    return;
  }

  /**
   * Duplicates a string as a new object.
   *
   * @param toClone String to clone.
   * @return Duplicate string of the argument string.
   */
  function String clone(String toClone) {
    var String ret;

    let ret = String.new(toClone.length());
    do Strings.appendToBuffer(ret, toClone);

    return ret;
  }

  /**
   * Disposes all the strings in an array along with the array itself.
   */
  function void disposeStringArray(Array array, int length) {
    var int i;
    var String string;
    let i = 0;
    while (i < length) {
      let string = array[i];
      do string.dispose();
      let i = i + 1;
    }
    do array.dispose();
    return;
  }

  /**
   * @return A string consisting of a substring repeated `amount` times.
   */
  function String repeat(String string, int amount) {
    var int i;
    var Array strings;
    var String glue, ret;

    // Handle case of repeating 0 or fewer times.
    if (~(amount > 0)) {
      return "";
    }

    let strings = Array.new(amount);
    let i = 0;
    while (i < amount) {
      let strings[i] = string;
      let i = i + 1;
    }

    let glue = "";
    let ret = Strings.join(strings, amount, glue);

    do strings.dispose();
    do glue.dispose();

    return ret;
  }

  /**
   * Concatenates an array of strings into a single string, joined by a
   * substring.
   *
   * @return A single joined string.
   */
  function String join(Array strings, int stringsLength, String glue) {
    var String ret, string;
    var int retLength, i;

    // Create a string buffer.
    let retLength = 0;
    let i = 0;
    while (i < stringsLength) {
      let string = strings[i];
      let retLength = retLength + string.length();
      let i = i + 1;
    }
    let retLength = retLength + ((stringsLength - 1) * glue.length());
    let ret = String.new(retLength);

    // For each string, append their chars to the buffer.
    let i = 0;
    while (i < stringsLength) {
      if (~(i = 0)) {
        do Strings.appendToBuffer(ret, glue);
      }
      let string = strings[i];
      do Strings.appendToBuffer(ret, string);
      let i = i + 1;
    }

    return ret;
  }

  /**
   * Concatcenates 2 strings together.
   *
   * @return The concatcenation of `a` and `b`.
   */
  function String concat(String a, String b) {
    var int stringsLength;
    var Array strings;
    var String glue, ret;

    let stringsLength = 2;
    let strings = Array.new(stringsLength);
    let strings[0] = a;
    let strings[1] = b;

    let glue = "";
    let ret = Strings.join(strings, stringsLength, glue);

    do strings.dispose();
    do glue.dispose();

    return ret;
  }

  /**
   * @return The string representation of an int.
   */
  function String coerceInt(int integer) {
    var int temp, length;
    var String buffer;

    // Determine the width of the number.
    let length = 1;
    let temp = Math.abs(integer);
    while ((temp / 10) > 0) {
      let length = length + 1;
      let temp = temp / 10;
    }

    // Negative numbers need space for a negative sign.
    if (integer < 0) {
      let length = length + 1;
    }

    let buffer = String.new(length);
    do buffer.setInt(integer);

    return buffer;
  }

  /**
   * @return The string representation of a char.
   */
  function String coerceChar(char character) {
    var String buffer;

    let buffer = String.new(1);
    do buffer.appendChar(character);

    return buffer;
  }

  /**
   * @return The string representation of a boolean.
   */
  function String coerceBoolean(boolean bool) {
    if (bool) {
      return "true";
    } else {
      return "false";
    }
  }

  /**
   * Gets a section of a string. Works like JavaScript's `substring()`.
   *
   * @param string String to get a section of.
   * @param beginIndex Starting index inclusive.
   * @param endIndex Ending index exclusive.
   * @return The section of the string from beginIndex to endIndex.
   */
  function String substring(String string, int beginIndex, int endIndex) {
    var String buffer;
    var int stringLength, temp, size, index;

    let stringLength = string.length();

    // Keep the indices in-bounds.
    let beginIndex = Math.max(0, beginIndex);
    let beginIndex = Math.min(stringLength, beginIndex);
    let endIndex = Math.max(0, endIndex);
    let endIndex = Math.min(stringLength, endIndex);

    // Handle the case of no range.
    if (beginIndex = endIndex) {
      return "";
    }

    // Swap indices if the arguments produce a negative range.
    if (beginIndex > endIndex) {
      let temp = beginIndex;
      let beginIndex = endIndex;
      let endIndex = temp;
    }

    let size = endIndex - beginIndex;
    let buffer = String.new(size);

    let index = beginIndex;
    while (index < endIndex) {
      do buffer.appendChar(string.charAt(index));
      let index = index + 1;
    }

    return buffer;
  }

  /**
   * Searches a string for a substring.
   *
   * @param beginIndex Usually `0`.
   * @return The index of the first occurrence of a substring in a string, or -1
   * if not found.
   */
  function int indexOf(String string, String substring, int beginIndex) {
    var int index, endIndex, stringLength, substringLength;
    var String toCheck;

    let index = beginIndex;
    let stringLength = string.length();
    let substringLength = substring.length();

    // Case of empty string. It is immediately found.
    if ((substringLength = 0) & ~(beginIndex > stringLength)) {
      return index;
    }

    while (index < stringLength) {
      let endIndex = index + substringLength;
      let toCheck = Strings.substring(string, index, endIndex);
      if (Strings.equals(substring, toCheck)) {
        do toCheck.dispose();
        return index;
      }
      do toCheck.dispose();
      let index = index + 1;
    }

    return -1;
  }

  /**
   * @return Number of occurrences of `substring` in `string`.
   */
  function int count(String string, String substring) {
    var int substringLength;
    var int count;
    var int index;
    var int nextIndex;

    let substringLength = substring.length();
    let count = 0;
    let index = 0;
    while (index > -1) {
      let nextIndex = Strings.indexOf(string, substring, index);
      if (nextIndex > -1) {
        let count = count + 1;
        // Guarantee that the index advances.
        let nextIndex = Math.max(nextIndex + substringLength,
                                 index + 1);
      }
      let index = nextIndex;
    }

    return count;
  }

  /**
   * Splits a string into an array of substrings where each substring is before
   * or after (and not including) a delimiter substring.
   *
   * @param string String to split.
   * @param delimiter Substring at which to split `string`.
   * @return Array of strings.
   */
  function Array split(String string, String delimiter) {
    var int index;
    var int occurrences;
    var int delimiterLength;
    var int retLength;
    var Array ret;
    var int i;
    var int nextIndex;

    let occurrences = Strings.count(string, delimiter);

    let delimiterLength = delimiter.length();
    let retLength = occurrences + 1;
    let ret = Array.new(retLength);
    let index = 0;
    let i = 0;
    while (i < occurrences) {
      // Guarantee that the index advances.
      let nextIndex = Math.max(Strings.indexOf(string, delimiter, index),
                               index + 1);
      let ret[i] = Strings.substring(string, index, nextIndex);
      let index = nextIndex + delimiterLength;
      let i = i + 1;
    }
    // Add the last substring.
    let ret[i] = Strings.substring(string, index, string.length());

    return ret;
  }

  /**
   * @param string String to parse.
   * @return The first char of a string.
   */
  function char parseChar(String string) {
    return string.charAt(0);
  }

  /**
   * Interpolates values into a string. Similar to C's `sprintf()`.
   *
   * Currently only supports "%i", "%d", "%s" and "%c", and no flags.
   *
   * @param format A string, potentially one containing "tag" substrings
   * of the form "%X".
   * @param values Array containing values of [potentially] many datatypes.
   * These values will replace the format tags in-order.
   * @return The format string interpolated with the given values.
   */
  function String sprintf(String format, Array values) {
    var int index, beginIndex, tagCount, substringsLength, i, substringsIndex;
    var Array substrings;
    var char specifier;
    var String percentSign, glue, ret;

    let percentSign = "%";

    let index = 0;
    let beginIndex = index;

    // Count how many tags there are so an array of the correct size
    // can be made.
    let tagCount = 0;
    while (~(index = -1)) {
      let index = Strings.indexOf(format, percentSign, beginIndex);
      if (index > -1) {
        let tagCount = tagCount + 1;
        let beginIndex = index + 2;
      }
    }

    // Handle case of having redundantly called this method.
    if (tagCount = 0) {
      return format;
    }

    let substringsLength = 1 + (tagCount * 2);
    let substrings = Array.new(substringsLength);

    let i = 0;
    let substringsIndex = 0;
    let index = 0;
    let beginIndex = index;

    while (~(index = -1)) {
      let index = Strings.indexOf(format, percentSign, beginIndex);
      if (index > -1) {
        // Get the substring before the tag.
        let substrings[substringsIndex] = Strings.substring(format, beginIndex,
                                                            index);
        let substringsIndex = substringsIndex + 1;

        // Update beginIndex to point past the tag.
        let beginIndex = index + 2;

        // Determine the next appropriate action based on a specifier.
        // e.g., "%i", "%s", "%c", etc.
        let specifier = format.charAt(index + 1);
        if ((specifier = 105) | (specifier = 100)) { // i || d
          let substrings[substringsIndex] = Strings.coerceInt(values[i]);
        } else {
          if (specifier = 115) { // s
            // TODO: Find alternative to cloning.
            let substrings[substringsIndex] = Strings.clone(values[i]);
          } else {
            if (specifier = 99) { // c
              let substrings[substringsIndex] = Strings.coerceChar(values[i]);
            }
          }
        }
        let substringsIndex = substringsIndex + 1;
        let i = i + 1;
      } else {
        let substrings[substringsIndex] = Strings.substring(format, beginIndex,
                                                            format.length());
      }
    }

    // Finally join all the formatted bits together.
    let glue = "";
    let ret = Strings.join(substrings, substringsLength, glue);

    do percentSign.dispose();
    do Strings.disposeStringArray(substrings, substringsLength);
    do glue.dispose();

    return ret;
  }

}
